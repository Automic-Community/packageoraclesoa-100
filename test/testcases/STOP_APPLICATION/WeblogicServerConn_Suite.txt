*** Settings ***
Documentation     NOTE:
...               add note here
Suite Setup       Setup_suite
Suite Teardown    Teardown_suite
Force Tags        STOP_APPLICATION
Default Tags      win    unix
Test Template     Stop Application Template
Resource          prt_keywords.txt
Resource          ../../resources/keywords.txt

*** Test Cases ***    OK_NOK    Host             Port           Username             Password                 Connection Protocol     Online Mode    RETURN CODE    RETURN STATUS
01_Success execute with Valid inputs
                      OK        ${_WL_HOST}      ${_WL_PORT}    ${_WL_ADMIN_USER}    ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    NO             0              ${ENDED_OK}

02_Failed execute with Invalid Host
                      NOK       Invalid_input    ${_WL_PORT}    ${_WL_ADMIN_USER}    ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    NO             1              ${ENDED_NOK}

03_Failed execute with Invalid Port
                      NOK       ${_WL_HOST}      1000           ${_WL_ADMIN_USER}    ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    NO             1              ${ENDED_NOK}

04_Failed execute with Invalid Admin User
                      NOK       ${_WL_HOST}      ${_WL_PORT}    Invalid_input        ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    NO             1              ${ENDED_NOK}

05_Failed execute with Invalid Admin Password
                      NOK       ${_WL_HOST}      ${_WL_PORT}    ${_WL_ADMIN_USER}    Invalid_input            ${_WL_CONN_PROTOCOL}    NO             1              ${ENDED_NOK}

06_Success execute with Valid inputs Online mode
                      OK        ${_WL_HOST}      ${_WL_PORT}    ${_WL_ADMIN_USER}    ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    YES             0              ${ENDED_OK}

07_Failed execute with Invalid Host Online mode
                      NOK       Invalid_input    ${_WL_PORT}    ${_WL_ADMIN_USER}    ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    YES             1              ${ENDED_NOK}

08_Failed execute with Invalid Port Online mode
                      NOK       ${_WL_HOST}      1000           ${_WL_ADMIN_USER}    ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    YES             1              ${ENDED_NOK}

09_Failed execute with Invalid Admin User Online mode
                      NOK       ${_WL_HOST}      ${_WL_PORT}    Invalid_input        ${_WL_ADMIN_PASSWORD}    ${_WL_CONN_PROTOCOL}    YES             1              ${ENDED_NOK}

10_Failed execute with Invalid Admin Password Online mode
                      NOK       ${_WL_HOST}      ${_WL_PORT}    ${_WL_ADMIN_USER}    Invalid_input            ${_WL_CONN_PROTOCOL}    YES             1              ${ENDED_NOK}

11_Failed execute with Invalid Connection Protocol Online mode
                      NOK       ${_WL_HOST}      ${_WL_PORT}    ${_WL_ADMIN_USER}    ${_WL_ADMIN_PASSWORD}    https                   YES            1              ${ENDED_NOK}

*** Keywords ***
Stop Application Template
    [Arguments]    ${OK_NOK}    ${WEBLOGIC_HOST}    ${WEBLOGIC_PORT}    ${WEBLOGIC_USERNAME}    ${WEBLOGIC_PASSWORD}    ${WEBLOGIC_USE_PROTOCOL}
    ...    ${SOA_ONLINE_MODE}    ${returnCode}    ${returnStatus}
    [Documentation]    This template is used for test STOP_APPLICATION with needed arguments
    ...    All inputs are default and valid, except input variables for WEBLOGIC CONNECTION promptset. Required inputs:
    ...    - ${WEBLOGIC_HOST} : IP or hostname of the Weblogic Server
    ...    - ${WEBLOGIC_PORT}: Port number used to connect to the Weblogic Server
    ...    - ${WEBLOGIC_USERNAME}: Username used to authenticate with WebLogic
    ...    - ${WEBLOGIC_PASSWORD}: Password used to authenticate with WebLogic
    ...    - ${WEBLOGIC_USE_PROTOCOL}: Select connection protocol to connect WLST to server
    ...    - ${returnCode}: expected return code after execution
    ...    - ${returnStatus}: expected return status after execution
    ...    - ${message}: expected message contained in report after execution
    Init Action    ${ACTION_STOP_APPLICATION}
    Action Set    ${PRT_WEBLOGIC_HOST}    ${WEBLOGIC_HOST}
    Action Set    ${PRT_WEBLOGIC_PORT}    ${WEBLOGIC_PORT}
    Action Set    ${PRT_WEBLOGIC_USERNAME}    ${WEBLOGIC_USERNAME}
    Action Set    ${PRT_WEBLOGIC_PASSWORD}    ${WEBLOGIC_PASSWORD}
    Action Set    ${PRT_WEBLOGIC_USE_PROTOCOL}    ${WEBLOGIC_USE_PROTOCOL}
    Set Default Valid Promptset
    Action Set    ${PRT_STOP_APPLICATION_ONLINE_MODE}    ${SOA_ONLINE_MODE}
    Action Set Timeout    500
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    ${returnCode}
    Action Return Status Should Be    ${returnStatus}
    Action Report Should Contain    ${header_logging}
    Action Report Should Contain    ${param_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${execution_logging}
    Run Keyword If    '${OK_NOK}'=='OK'    Action Report Should Contain    ${footer_logging_OK}

Setup_suite
    [Documentation]    TODO: Write all steps need prepare before testing here
    Log    Stop testing Server Connection for Stop Application Template
    Deploy a Composite Application
    Start a Composite Application

Teardown_suite
    [Documentation]    TODO: Write all steps for cleanup after testing here
    Log    Testing Server Connection for Stop Application Template end
    Undeploy a Composite Application
